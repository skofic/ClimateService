///
// Combine properties into dump.
///
FOR doc IN @@collection
    LET properties = doc.properties
    COLLECT longitude = doc.lon,
            latitude = doc.lat,
            date = doc.std_date
    INTO data
    KEEP properties
    LET vars = MERGE_RECURSIVE(data[*].properties)
    LET spot = GEO_POINT(longitude, latitude)
    LET area = GEO_POLYGON([
        [ longitude-@radius, latitude-@radius ],
        [ longitude+@radius, latitude-@radius ],
        [ longitude+@radius, latitude+@radius ],
        [ longitude-@radius, latitude+@radius ],
        [ longitude-@radius, latitude-@radius ]
    ])
RETURN {
    _key: CONCAT(MD5(TO_STRING(area)), date),
    geometry_hash: MD5(TO_STRING(area)),
	geometry_point: spot,
    geometry_bounds: area,
    geometry_point_radius: @radius,
	std_date: date,
	properties: vars,
	std_terms: ATTRIBUTES(vars)
}
