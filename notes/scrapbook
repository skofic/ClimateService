//
// pr - PROCESS STEP 1.
//
LET documents_pr = [
    { lon: 12.23, lat: 45.12, jan: 1.0, feb: 2.0, mar: 3.0 },
    { lon: 12.15, lat: 45.13, jan: 1.1, feb: 2.1, mar: 3.1 },
    { lon: 13.42, lat: 45.04, jan: 1.2, feb: 2.2, mar: 3.2 },
    { lon: 7.274, lat: 46.11, jan: 1.3, feb: 2.3, mar: 3.4 }
]

//
// tas - PROCESS STEP 1.
//
LET documents_tas = [
    { lon: 12.23, lat: 45.12, jan: 10.0, feb: 20.0, mar: 30.0 },
    { lon: 12.15, lat: 45.13, jan: 10.1, feb: 20.1, mar: 30.1 },
    { lon: 13.42, lat: 45.04, jan: 10.2, feb: 20.2, mar: 30.2 },
    { lon: 7.274, lat: 46.11, jan: 10.3, feb: 20.3, mar: 30.4 }
]

//
// pr - PROCESS STEP 2 ==> in JSONL dump in data folder.
//
LET batch_1 = (
	FOR doc IN documents_pr
	RETURN { lon: doc.lon, lat: doc.lat, pr: VALUES(UNSET(doc, '_id', '_key', '_rev', 'lon', 'lat'))}
)

//
// tas - PROCESS STEP 2 ==> in JSONL dump in data folder.
//
LET batch_2 = (
	FOR doc IN documents_tas
	RETURN { lon: doc.lon, lat: doc.lat, tas: VALUES(UNSET(doc, '_id', '_key', '_rev', 'lon', 'lat'))}
)

LET documents = APPEND(batch_1, batch_2)

//
// Merge.
//
FOR doc IN documents
	COLLECT longitude = doc.lon, latitude = doc.lat INTO items
	
	LET data = UNSET(MERGE(items[*].doc), 'lon', 'lat')
	LET keys = ATTRIBUTES(data)
	LET geo = GEO_POINT(longitude, latitude)
	
	FOR month IN 1..3
	    LET values = (
	        FOR key IN keys
	        RETURN data[key][month - 1]
	    )
	    
	    RETURN {
    	    _key: MD5(TO_STRING(geo)),
    	    geometry: geo,
    	    properties: {
    		    monthly: MERGE({std_month: month}, ZIP(keys, values))
    		    
    	    }
	    }
