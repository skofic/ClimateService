//
// pr - PROCESS STEP 1.
//
LET documents_pr = [
    { lon: 12.23, lat: 45.12, '01': 1.0, '02': 2.0, '03': 3.0 },
    { lon: 12.15, lat: 45.13, '01': 1.1, '02': 2.1, '03': 3.1 },
    { lon: 13.42, lat: 45.04, '01': 1.2, '02': 2.2, '03': 3.2 },
    { lon: 7.274, lat: 46.11, '01': 1.3, '02': 2.3, '03': 3.4 }
]

//
// tas - PROCESS STEP 1.
//
LET documents_tas = [
    { lon: 12.23, lat: 45.12, '01': 10.0, '02': 20.0, '03': 30.0 },
    { lon: 12.15, lat: 45.13, '01': 10.1, '02': 20.1, '03': 30.1 },
    { lon: 13.42, lat: 45.04, '01': 10.2, '02': 20.2, '03': 30.2 },
    { lon: 7.274, lat: 46.11, '01': 10.3, '02': 20.3, '03': 30.4 }
]

//
// pr - PROCESS STEP 2 ==> in JSONL dump in data folder.
//
LET batch_1 = (
	FOR doc IN documents_pr
	RETURN { lon: doc.lon, lat: doc.lat, pr: VALUES(UNSET(doc, '_id', '_key', '_rev', 'lon', 'lat'))}
)

//
// tas - PROCESS STEP 2 ==> in JSONL dump in data folder.
//
LET batch_2 = (
	FOR doc IN documents_tas
	RETURN { lon: doc.lon, lat: doc.lat, tas: VALUES(UNSET(doc, '_id', '_key', '_rev', 'lon', 'lat'))}
)

LET documents = APPEND(batch_1, batch_2)

//
// Merge.
//
FOR doc IN documents
	COLLECT longitude = doc.lon, latitude = doc.lat INTO items
	
	LET geo = GEO_POINT(longitude, latitude)
	
	LET data = MERGE(items[*].doc)
	
	LET months = (
        FOR month IN 1..3
            RETURN MERGE(
                {
                    std_month: month,
                },
                {
                    pr: data.pr[month - 1],
                    tas: data.tas[month - 1]
                }
            )
	)
	
	RETURN {
    	_key: MD5(TO_STRING(geo)),
    	geometry: geo,
    	properties: {
    		monthly: months
    	}
	}
