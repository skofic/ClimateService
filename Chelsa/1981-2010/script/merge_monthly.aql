///
// Combine all 1981-2010 monthly data into the Chelsa_1981_2010_monthly collection,
// create the point geometry and hash it to become the _key.
// Remember to clear the collection before.
///
FOR doc IN temp_pong
    COLLECT longitude = doc.lon, latitude = doc.lat INTO items

    LET point_data = (
        FOR doc IN items[*].doc
        RETURN UNSET(doc, '_id', '_key', '_rev', 'lon', 'lat')
    )
    
    LET month_data = (
        FOR doc IN point_data
            COLLECT month = doc.std_month INTO months
        RETURN MERGE(months[*].doc)
    )
    
    LET geo = GEO_POINT(longitude, latitude)

INSERT {
    _key: MD5(TO_STRING(geo)),
    geometry: geo,
    properties: {
        `1981-2010`: {
            monthly: month_data
        }
    }
} IN Chelsa_1981_2010_monthly
